/*
 * Master Gradle build script
 *
 * Called by build.gradle in the root of the workspace to configure the project set.
 *
 * Depends on bndURI and bndWorkspace properties set by settings.gradle.
 */

/* Add bnd as a script dependency */
buildscript {
    repositories {
		jcenter()
    }
  dependencies {
    classpath files(bndURI, 'cnf/gradle/dependencies/org.osgi.impl.bundle.repoindex.cli.jar'), 
		'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
  }
}

/* Load the BndPlugin class */
apply from: rootProject.file('cnf/gradle/BndPlugin.gradle')

allprojects {
	apply plugin: nl.javadude.gradle.plugins.license.LicensePlugin
	
	/* Configure the license plugin options */
	license {
		header rootProject.file('etc/classheader_plain.txt')
		excludes(["**/*.txt", "**/*.html", "packageinfo"])
		skipExistingHeaders false
	}
}

/* Configure the subprojects */
subprojects {
  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
    plugins.apply BndPlugin
  }
}